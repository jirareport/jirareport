buildscript {
    ext {
        springBootVersion = "2.0.2.RELEASE"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "checkstyle"
apply plugin: "pmd"

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

mainClassName = "br.com.leonardoferreira.jirareport.Application"

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

ext {
    gsonVersion = "2.8.1"
    httpClientVersion = "4.5.1"
    springCloudVersion = "Finchley.RC1"
    hibernateTypesVersion = "2.2.0"
    mapstructVersion = "1.2.0.Final"
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-cache")

    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect")
    compile("org.thymeleaf.extras:thymeleaf-extras-springsecurity4")

    compile("org.springframework.boot:spring-boot-devtools")

    compile("org.apache.httpcomponents:httpclient:${httpClientVersion}")
    compile("com.google.code.gson:gson:${gsonVersion}")

    compile("org.springframework.cloud:spring-cloud-starter-openfeign")
    compile("org.springframework.cloud:spring-cloud-starter-sleuth")

    runtime("org.postgresql:postgresql")

    compile("com.vladmihalcea:hibernate-types-52:${hibernateTypesVersion}")

    compile("org.mapstruct:mapstruct-jdk8:${mapstructVersion}")
    compile("org.mapstruct:mapstruct-processor:${mapstructVersion}")

    compileOnly("org.projectlombok:lombok")

    compile("org.flywaydb:flyway-core:5.0.7")

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

checkstyle {
    toolVersion = "5.9"
    configFile = new File(rootDir, "checkstyle.xml")
}

pmd {
    toolVersion = "6.0.0"
    ruleSets = [
            "java-unusedcode",
            "java-basic",
            "java-design"
    ]
}

/**
 * example:
 * ./gradlew createMigration -Pmigration=setup_database
 */
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter;
task createMigration() {
    doLast {
        def timestamp = LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyyMMddHHmm"))
        System.out.println("$projectDir")
        def file = new File("$projectDir/src/main/resources/db/migration/V${timestamp}__${migration}.sql")
        file.createNewFile();
    }
}
